buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.jengelman.gradle.plugins:shadow:6.1.0'
    }
}

plugins {
    id 'java'
    id "com.github.spotbugs" version "4.7.1" apply false
    id 'org.cadixdev.licenser' version '0.6.1' apply false
}

allprojects {
    apply plugin: "com.github.spotbugs"
    compileJava.options.encoding = 'UTF-8'

    group = 'net.elytrium'
    version = '1.0.0'

    ext {
        // dependency versions
        textVersion = '3.0.4'
        adventureVersion = '4.8.1'
        junitVersion = '5.7.0'
        slf4jVersion = '1.7.30'
        log4jVersion = '2.13.3'
        nettyVersion = '4.1.58.Final'
        guavaVersion = '25.1-jre'
        checkerFrameworkVersion = '3.6.1'
        configurateVersion = '3.7.1'

        getCurrentShortRevision = {
            new ByteArrayOutputStream().withStream { os ->
                exec {
                    executable = "git"
                    args = ["rev-parse", "HEAD"]
                    standardOutput = os
                }
                return os.toString().trim().substring(0, 8)
            }
        }
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    repositories {
        mavenLocal()
        mavenCentral()

        // for kyoripowered dependencies
        maven {
            url 'https://oss.sonatype.org/content/groups/public/'
        }

        maven {
            name = 'velocitypowered-repo'
            url = 'https://repo.velocitypowered.com/snapshots/'
        }

        maven {
            name = 'minecraft-libraries'
            url = 'https://libraries.minecraft.net/'
        }

        maven {
            name = 'spongepowered-repo'
            url = 'https://repo.spongepowered.org/maven'
        }
    }

    test {
        reports {
            junitXml.enabled = true
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(com.github.spotbugs.snom.SpotBugsTask) {
        reports {
            html {
                enabled = true
                destination = file("$buildDir/reports/spotbugs/main/spotbugs.html")
                stylesheet = 'fancy-hist.xsl'
            }
        }
    }
}
