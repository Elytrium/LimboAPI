plugins {
  id("java")
  id("checkstyle")
  id("com.github.spotbugs").version("5.0.7").apply(false)
  id("org.cadixdev.licenser").version("0.6.1").apply(false)
}

allprojects {
  apply(plugin: "checkstyle")
  apply(plugin: "com.github.spotbugs")
  apply(plugin: "org.cadixdev.licenser")

  setGroup("net.elytrium")
  setVersion("1.0.6-SNAPSHOT")

  compileJava {
    getOptions().setEncoding("UTF-8")
  }

  java {
    setSourceCompatibility(JavaVersion.VERSION_11)
    setTargetCompatibility(JavaVersion.VERSION_11)
  }

  repositories {
    mavenCentral()

    maven {
      setName("elytrium-repo")
      setUrl("https://maven.elytrium.net/repo/")
    }
    maven {
      setName("papermc-repo")
      setUrl("https://papermc.io/repo/repository/maven-public/")
    }
  }

  checkstyle {
    setToolVersion("10.3")
    setConfigFile(file("${this.getRootDir()}/config/checkstyle/checkstyle.xml"))
    setConfigProperties("configDirectory": "${this.getRootDir()}/config/checkstyle")
    setMaxErrors(0)
    setMaxWarnings(0)
  }

  spotbugs {
    setExcludeFilter(file("${this.getRootDir()}/config/spotbugs/suppressions.xml"))
  }

  spotbugsMain {
    reports {
      html {
        getRequired().set(true)
        getOutputLocation().set(file("${this.getBuildDir()}/reports/spotbugs/main/spotbugs.html"))
        setStylesheet("fancy-hist.xsl")
      }
    }
  }
}

String getCurrentShortRevision() {
  OutputStream outputStream = new ByteArrayOutputStream()
  exec {
    if (System.getProperty("os.name").startsWith("Windows")) {
      commandLine("cmd", "/c", "git rev-parse HEAD")
    } else {
      commandLine("bash", "-c", "git rev-parse HEAD")
    }

    setStandardOutput(outputStream)
  }

  return outputStream.toString().trim().substring(0, 8)
}
