//file:noinspection GroovyAssignabilityCheck

plugins {
    id("java")
    id("checkstyle")
    id("com.github.spotbugs").version("5.0.5").apply(false)
    id("org.cadixdev.licenser").version("0.6.1").apply(false)
}

allprojects {
    apply plugin: "checkstyle"
    apply plugin: "com.github.spotbugs"
    apply plugin: "org.cadixdev.licenser"

    setGroup("net.elytrium")
    setVersion("1.0.4-SNAPSHOT")

    compileJava {
        getOptions().setEncoding("UTF-8")
    }

    java {
        setSourceCompatibility(JavaVersion.VERSION_11)
        setTargetCompatibility(JavaVersion.VERSION_11)
    }

    repositories {
        mavenCentral()

        maven {
            setName("elytrium-repo")
            setUrl("https://maven.elytrium.net/repo/")
        }
        maven {
            setName("papermc-repo")
            setUrl("https://papermc.io/repo/repository/maven-public/")
        }
    }

    checkstyle {
        setToolVersion("9.2.1")
        setConfigFile(file("${this.getRootDir()}/config/checkstyle/checkstyle.xml"))
        setConfigProperties("configDirectory": "${this.getRootDir()}/config/checkstyle")

        // The build should immediately fail if we have errors.
        setMaxErrors(0)
        setMaxWarnings(0)
    }

    spotbugs {
        setExcludeFilter(file("${this.getRootDir()}/config/spotbugs/suppressions.xml"))
    }

    spotbugsMain {
        reports {
            html {
                getRequired().set(true)
                getOutputLocation().set(file("${this.getBuildDir()}/reports/spotbugs/main/spotbugs.html"))
                setStylesheet("fancy-hist.xsl")
            }
        }
    }
}
