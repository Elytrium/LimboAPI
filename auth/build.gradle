import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java'
    id 'checkstyle'
}

apply plugin: 'org.cadixdev.licenser'
apply from: '../gradle/checkstyle.gradle'
apply plugin: 'com.github.johnrengelman.shadow'

license {
    matching(includes: ['**/limboauth/config/helpers/**']) {
        header = project.rootProject.file('HEADER_BUNGEECORD.txt')
    }
    header = project.rootProject.file('HEADER.txt')
}

dependencies {
    compileOnly project(':limboapi-api')
    compileOnly files('../libs/3.0.0.jar')

    implementation 'at.favre.lib:bcrypt:0.9.0'
    implementation 'dev.samstevens.totp:totp:1.7.1'
    implementation 'com.j256.ormlite:ormlite-jdbc:5.6'

    implementation 'com.h2database:h2:1.4.200'
    implementation "org.mariadb.jdbc:mariadb-java-client:2.7.3"
    implementation 'org.xerial:sqlite-jdbc:3.36.0.1'

    annotationProcessor 'com.velocitypowered:velocity-api:3.0.0-SNAPSHOT'

    compileOnly 'org.projectlombok:lombok:1.18.20'
    annotationProcessor 'org.projectlombok:lombok:1.18.20'

    testCompileOnly 'org.projectlombok:lombok:1.18.20'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.20'

    compileOnly 'com.github.spotbugs:spotbugs-annotations:4.1.2'
}

task processSources(type: Sync) {
    from sourceSets.main.java.srcDirs
    inputs.property 'version', version
    filter ReplaceTokens, tokens: [version: version]
    into "$buildDir/src"
}

tasks.withType(Checkstyle) {
    exclude('**/net/elytrium/limboauth/config/Settings.java')
}

artifacts {
    archives shadowJar
}

compileJava.source = processSources.outputs
